--| TABLE: GA4 Events Production Data Mart
--| DESCRIPTION: Consolidates all event attributes into a single flat table for reporting
--| UNIQUE KEY: event_id

config {
  type: "incremental",
  schema: 'df_warehouse_output',
  assertions: {
    uniqueKey: ["event_id"]
  },
  tags: ["daily"],
  bigquery: {
    partitionBy: "event_date",
    clusterBy: ["event_timestamp"]
  }
}

pre_operations {

  DECLARE event_date_checkpoint DEFAULT (
    SELECT date("2020-01-01")
  );

  SET event_date_checkpoint = (
    ${when(incremental(),
      `SELECT
        LEAST(
          (SELECT date_sub(current_date(), INTERVAL ${dataform.projectConfig.vars.ga4_days_back} DAY)),
          (SELECT max(event_date) from ${self()})
        )`,
      `SELECT date("${dataform.projectConfig.vars.ga4_start_date}")`
    )}
  );

  ${when(incremental(),
    `DELETE ${self()} WHERE event_date > event_date_checkpoint`
  )}  
}

--Pull all rows in the selected date range from the intermediate events table with the appropriate timezone applied
WITH events_table AS (
  SELECT * EXCEPT(

    --Exclude items
    items,

    --Exclude session-scoped attributes (included in sessions view)
    referring_domain,
    is_active_user,
    ga_session_number,
    session_engaged,
    device_category,
    device_language,
    country,
    city,
    metro,
    region,
    sub_continent,
    continent,
    mobile_brand_name,
    mobile_model_name,
    mobile_marketing_name,
    mobile_os_hardware_model,
    operating_system,
    operating_system_version,
    vendor_id,
    advertising_id,
    is_limited_ad_tracking,
    browser,
    browser_version,
    platform,

    --Exclude user-scoped attributes (included in users view)
    user_id,
    analytics_storage,
    ads_storage,
    uses_transient_token,
    first_user_campaign,
    first_user_medium,
    first_user_source,
    user_first_touch_timestamp
  )
  FROM ${ref("int_ga4_events")}
  WHERE event_date > event_date_checkpoint
)

SELECT * 
FROM events_table
