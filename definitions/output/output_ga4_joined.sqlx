--| TABLE: GA4 Production Data Mart
--| DESCRIPTION: Joins all production data marts into a master
--| UNIQUE KEY: event_id

config {
  type: "incremental",
  schema: 'df_warehouse_output',
  tags: ["daily"],
  bigquery: {
    partitionBy: "event_date",
    clusterBy: ["primary_user_id","full_session_id"]
  },
  columns: {
    user_pseudo_id: "Identifies the user's device as written to a first-party cookie.",
    primary_user_id: "Identifies the user's with the user_id if available, and defaults to the device id as written to a first-party cookie.",
    stream_id: "Identifies the Google Analytics 4 stream that collected the events (associated with the measurement ID).",
    event_bundle_sequence_id: "The sequential ID of the bundle in which these events were uploaded.",
    event_date: "The date that the event was collected with the selected timezone applied.",
    event_server_timestamp_offset: "Timestamp offset between collection time and upload time in micros.",
    user_id: "The identifier that is associated with the user when authenticated in the first party customer database. Used to identify a user across devices.",
    analytics_storage: "Whether Analytics storage is enabled for the user. Possible values: Yes, No, Unset",
    ads_storage: "Whether ad targeting is enabled for a user. Possible values: Yes, No, Unset",
    uses_transient_token: "Whether a web user has denied Analytics storage and the developer has enabled measurement without cookies based on transient tokens in server data. Possible values: Yes, No, Unset",
    user_first_touch_timestamp: "The time at which the user first opened the app or visited the site.",
    ltv_revenue: "The Lifetime Value (revenue) of the user. This field is not populated in intraday tables.",
    ltv_currency: "The Lifetime Value (currency) of the user. This field is not populated in intraday tables.",
    device_category: "The device category (mobile, tablet, desktop).",
    mobile_brand_name: "The device brand name.",
    mobile_model_name: "The device model name.",
    mobile_marketing_name: "The device marketing name.",
    mobile_os_hardware_model: "The device model information retrieved directly from the operating system.",
    operating_system: "The operating system of the device.",
    operating_system_version: "The version of the operating system of the device.",
    vendor_id: "IDFV (present only if IDFA is not collected).",
    advertising_id: "Advertising ID/IDFA.",
    device_language: "The OS language.",
    is_limited_ad_tracking: "The device's Limit Ad Tracking setting. On iOS14+, returns false if the IDFA is non-zero.",
    browser: "The browser in which the user viewed content.",
    browser_version: "The version of the browser in which the user viewed content.",
    hostname: "The hostname associated with the logged event.",
    city: "The city from which events were reported, based on IP address.",
    metro: "The metro from which events were reported, based on IP address.",
    region: "The region from which events were reported, based on IP address.",
    country: "The country from which events were reported, based on IP address.",
    sub_continent: "The subcontinent from which events were reported, based on IP address.",
    continent: "The continent from which events were reported, based on IP address.",
    app_id: "The package name or bundle ID of the app.",
    app_version: "The app's versionName (Android) or short bundle version.",
    install_store: "",
    firebase_app_id: "The Firebase App ID associated with the app.",
    install_source: "The store that installed the app.",
    is_active_user: "Whether the user was active (True) or inactive (False) at any point in the calendar day.",
    first_user_campaign: "Name of the marketing campaign that first acquired the user. This field is not populated in intraday tables.",
    first_user_medium: "Name of the medium (paid search, organic search, email, etc.) that first acquired the user. This field is not populated in intraday tables.",
    first_user_source: "Name of the network that first acquired the user. This field is not populated in intraday tables.",
    platform: "The data stream platform (Web, IOS or Android) from which the event originated.",
    total_item_quantity: "Total number of items in this event, which is the sum of items.quantity.",
    purchase_revenue_in_usd: "Purchase revenue of this event, represented in USD with standard unit. Populated for purchase event only.",
    purchase_revenue: "Purchase revenue of this event, represented in local currency with standard unit. Populated for purchase event only.",
    refund_value_in_usd: "The amount of refund in this event, represented in USD with standard unit. Populated for refund event only.",
    refund_value: "The amount of refund in this event, represented in local currency with standard unit. Populated for refund event only.",
    shipping_value_in_usd: "The shipping cost in this event, represented in USD with standard unit.",
    shipping_value: "The shipping cost in this event, represented in local currency.",
    tax_value_in_usd: "The tax value in this event, represented in USD with standard unit.",
    tax_value: "The tax value in this event, represented in local currency with standard unit.",
    unique_items: "The number of unique items in this event, based on item_id, item_name, and item_brand.",
    ecommerce_transaction_id: "The transaction ID of the ecommerce transaction.",
    transaction_id: "The transaction ID of the ecommerce transaction.",
    event_manual_campaign_id: "The manual campaign id (utm_id) that was collected with the event.",
    event_manual_campaign_name: "The manual campaign name (utm_campaign) that was collected with the event.",
    event_manual_source: "The manual campaign source (utm_source) that was collected with the event. Also includes parsed parameters from referral params, not just UTM values. ",
    event_manual_medium: "The manual campaign medium (utm_medium) that was collected with the event. Also includes parsed parameters from referral params, not just UTM values.",
    event_manual_term: "The manual campaign keyword/term (utm_term) that was collected with the event.",
    event_manual_content: "The additional manual campaign metadata (utm_content) that was collected with the event.",
    event_gclid: "The Google click identifier that was collected with the event.",
    event_dclid: "The Google Marketing Platform (GMP) identifier that was collected with the event.",
    event_srsltid: "The Google Merchant Center identifier that was collected with the event.",
    event_gbraid: "An alternative to gclid, the GBRAID is used for app-to-app measurement.",
    event_wbraid: "An alternative to gclid, the WBRAID is used for web-to-app measurement.",
    event_fbclid: "The Facebook Click ID parameter collected with the event.",
    partition_date: "The date used by sharded tables in BigQuery.",
    page_referrer: "The URL of the prior page that was loaded before the event was logged.",
    session_engaged: "Set to '1' if the client library has determined that the current session has met the criteria to be considered 'engaged', otherwise set to '0'.",
    link_classes: "The class names associated with the <a> tag that was interacted with by the user.",
    link_domain: "The domain of the href property associated with the <a> tag that was interacted with by the user.",
    link_id: "The id associated with the <a> tag that was interacted with by the user.",
    link_url: "The href property associated with the <a> tag that was interacted with by the user.",
    outbound: "Boolean field that determines if the link that the user selected will direct them to a separate domain.",
    file_extension: "The type of file associated with a download event.",
    file_name: "The name of the file that was interacted with by the user.",
    link_text: "The text of the link that was interacted with by the user.",
    form_id: "The id associated with the <form> tag that was interacted with by the user.",
    form_name: "The name associated with the <form> tag that was interacted with by the user.",
    form_destination: "The destination of the <form> tag that was interacted with by the user.",
    form_submit_text: "The text of the button that was clicked when the form was submitted.",
    video_current_time: "The current time of the video in progress.",
    video_duration: "The duration of the video that was interacted with by the user.",
    video_percent: "The current progress of the user through the video, measured by % complete.",
    video_provider: "The name of the current video's provider.",
    video_url: "The url of the video that the user interacted with.",
    visible: "Identifies if the element was visible at the time the event was logged.",
    currency: "The currency of the value.",
    value: "The monetary value associated with the event.",
    coupon: "The name of the coupon that was applied by the user.",
    payment_type: "The payment type submitted by the user.",
    shipping_tier: "The shipping tier selected by the user.",
    virtual_currenty_name: "The name of the virtual currency.",
    shipping: "The amount that the user paid for shipping with the purchase.",
    tax: "The amount that the user paid for tax with the purchase.",
    item_list_id: "The identifier of the item list that was interacted with.",
    item_list_name: "The name of the item list that was interacted with.",
    creative_name: "The name of the creative that was interacted with.",
    creative_slot: "The position of the creative that was interacted with.",
    promotion_id: "The identifier of the promotion that was interacted with.",
    promotion_name: "The name of the promotion that was interacted with.",
    level_name: "The name of the level where the event was logged.",
    character: "The name of the character that the user interacted with.",
    score: "The user's score at the time the event was logged.",
    achievement_id: "The identifier of the achievement that the user received.",
    group_id: "The identifier of the group that the user interacted with.",
    method: "The method associated with the user interaction.",
    search_term: "The internal search term applied by the user.",
    content_type: "The type of content that the user interacted with.",
    content_id: "The identifier of the content that was interacted with.",
    item_id: "The id of the item that was interacted with.",
    session_date: "The date when the session was recorded.",
    ga_session_id: "An identifier for the session that is unique to each device.",
    ga_session_number: "Counts the number of sessions that this user has had. A new user is equal to 1.",
    session_start_timestamp: "The timestamp of the first event of the user's first session.",
    full_session_id: "A unique identifier for the session (combines user_pseudo_id and ga_session_id).",
    first_event_medium: "The first UTM Medium value associated with this user.",
    first_event_source: "The first UTM Source value associated with this user.",
    first_event_campaign: "The first UTM Campaign value associated with this user.",
    session_source: "The most recent UTM Source value associated with this user, using last non-direct attribution.",
    session_medium: "The most recent UTM Medium value associated with this user, using last non-direct attribution.",
    session_campaign_name: "The most recent UTM Campaign value associated with this user, using last non-direct attribution.",
    session_campaign_id: "The most recent UTM ID value associated with this user, using last non-direct attribution.",
    session_term: "The most recent UTM Term value associated with this user, using last non-direct attribution.",
    session_content: "The most recent UTM Content value associated with this user, using last non-direct attribution.",
    session_gclid: "The most recent GCLID value [Google Ads] associated with this user, using last non-direct attribution.",
    session_dclid: "The most recent DCLID value [Floodlight] associated with this user, using last non-direct attribution.",
    session_srsltid: "The most recent SRSLTID value [Merchant Center] associated with this user, using last non-direct attribution.",
    session_wbraid: "The most recent WBRAID value associated with this user, using last non-direct attribution.",
    session_fbclid: "The most recent FBCLID value associated with this user, using last non-direct attribution.",
    session_gbraid: "The most recent GBRAID value associated with this user, using last non-direct attribution.",
    device_category: "The type of device that was used when the session was recorded [mobile, desktop, tablet].",
    engaged_session: "Identifies the session as 'engaged' using logic recorded in the browser.",
    session_type: "Identifies the session as 'New' or 'Returning' based on the ga_session_number field.",
    segment_demo_page: "Marked as true when a session includes events collected on the demo page (URL contains '/demo').",
    session_duration: "Subtracts the timestamp of the last event of the session from the timestamp of the first.",
    landing_page: "The URL of the first page_view recorded during the session.",
    landing_page_title: "The title of the first page_view recorded during the session.",
    page_location: "The URL of the page where the event was fired.",
    page_title: "The title of the page where the event was fired.",
    referring_domain: "The domain of the referring source that directed the user to the site.",
    video_title: "The title of the video that was interacted with.",
    event_id: "A unique identifier for the event.",
    event_name: "The name of the event.",
    engagement_time_msec: "The number of milliseconds that the browser tab has been active since the last recording.",
    event_number: "During instances where the same event name was captured multiple times and then recorded in a single hit with the same timestamp, this field will order the events.",
    session_event_number: "The sequence that the events appeared in during the session.",
    user_event_number: "The sequence that the events appeared in during the user's history of interactions across sessions.",
    event_timestamp: "The timestamp of the event.",
    session_first_traffic_source: "All traffic source data [struct] associated with the first events of the session.",
    session_traffic_source_last_non_direct: "All traffic source data [struct] associated with the last events of the session.",
    session_start: "The timestamp associated with the first event of the session.",
    session_end: "The timestamp associated with the last event of the session.",
    example_user_property: "An example parameter to show how a user property can be added to the data mart.",
    first_session_date: "The date of the first session generated by the user.",
    most_recent_session_date: "The data of the most recent session generated by the user.",
    most_recent_device_category: "The most recent device category used by the user.",
    most_recent_device_mobile_brand_name: "The brand name of the mobile device used during the user's most recent session.",
    most_recent_device_mobile_model_name: "The model name of the mobile device used during the user's most recent session.",
    most_recent_device_mobile_marketing_name: "The marketing name of the mobile device used during the user's most recent session.",
    most_recent_device_mobile_os_hardware_model: "The OS hardware model of the mobile device used during the user's most recent session.",
    most_recent_device_operating_system: "The OS of the device used during the user's most recent session.",
    most_recent_device_operating_system_version: "The OS version of the device used during the user's most recent session.",
    most_recent_device_vendor_id: "The IDFV (present only if IDFA is not collected) of the device used during the user's most recent session.",
    most_recent_device_advertising_id: "The Advertising ID/IDFA of the device used during the user's most recent session.",
    most_recent_device_language: "The language of the device used during the user's most recent session.",
    most_recent_device_is_limited_ad_tracking: "The device's Limit Ad Tracking setting of the device used during the user's most recent session. On iOS14+, returns false if the IDFA is non-zero.",
    most_recent_device_browser: "The browser in which the user viewed content during the most recent session.",
    most_recent_device_browser_version: "The version of the browser in which the user viewed content during the most recent session.",
    most_recent_device_hostname: "The last hostname associated with the events of the most recent session.",
    most_recent_geo_city: "The city from which events were reported during the user's most recent session, based on IP address.",
    most_recent_geo_metro: "The metro from which events were reported during the user's most recent session, based on IP address.",
    most_recent_geo_region: "The region from which events were reported during the user's most recent session, based on IP address.",
    most_recent_geo_country: "The country from which events were reported during the user's most recent session, based on IP address.",
    most_recent_geo_sub_continent: "The subcontinent from which events were reported during the user's most recent session, based on IP address.",
    most_recent_geo_continent: "The continent from which events were reported during the user's most recent session, based on IP address.",
    most_recent_appInfo_id: "The package name or bundle ID of the app captured during the user's most recent session.",
    most_recent_appInfo_version: "The app's versionName (Android) or short bundle version captured during the user's most recent session.",
    most_recent_appInfo_install_store: "",
    most_recent_appInfo_firebase_app_id: "The Firebase App ID associated with the app during the user's most recent session.",
    most_recent_appInfo_install_source: "The store that installed the app most recently before the user's most recent session.",
    total_views: "The sum of page_view/screen_view events generated by the user during the evaluated time period.",
    lifetime_revenue: "The sum of revenue generated by the user.",
    lifetime_sessions: "The sum of sessions generated by the user.",
    total_events: "The sum of events generated by the user during the evaluated time period.",
    total_engagement_time: "The sum of engagement time generated by the user during the evaluated time period.",
    total_sessions: "The sum of sessions generated by the user during the evaluated time period.",
    total_clicks: "The sum of click events generated by the user during the evaluated time period.",
    total_scrolls: "The sum of scroll events [90% of page] generated by the user during the evaluated time period.",
    total_internal_searches: "The sum of internal searches completed by the user during the evaluated time period.",
    total_videos_started: "The sum of video_start events initiated by the user during the evaluated time period.",
    total_videos_completed: "The sum of videos played until completion by the user during the evaluated time period.",
    total_files_downloaded: "The count of files downloaded by the user during the evaluated time period.",
    total_forms_started: "The count of forms that the user started to complete during the evaluated time period.",
    total_forms_submitted: "The count of forms that the user submitted during the evaluated time period.",
    total_item_lists_viewed: "The count of item lists that the user viewed during the evaluated time period.",
    total_items_selected: "The count of items that the user selected during the evaluated time period.",
    total_items_viewed: "The count of items that the user viewed during the evaluated time period.",
    total_adds_to_cart: "The count of add_to_cart events that the user generated during the evaluated time period.",
    total_adds_to_wishlist: "The count of add_to_wishlist events that the user generated during the evaluated time period.",
    total_carts_viewed: "The count of view_cart events that the user generated during the evaluated time period.",
    total_removes_from_cart: "The count of remove_from_cart events that the user generated during the evaluated time period.",
    total_checkouts_started: "The count of begin_checkout events that the user generated during the evaluated time period.",
    total_purchases: "The count of purchase events that the user generated during the evaluated time period.",
    total_refunds: "The count of refund events associated with the user during the evaluated time period.",
    total_promos_viewed: "The count of view_promotion events that the user generated during the evaluated time period.",
    total_promos_selected: "The count of select_promotion events that the user generated during the evaluated time period.",
    total_leads_generated: "The count of lead_generated events that the user generated during the evaluated time period.",
    total_logins: "The count of login events that the user generated during the evaluated time period.",
    total_searches: "The count of search events that the user generated during the evaluated time period.",
    total_shares: "The count of share events that the user generated during the evaluated time period.",
    total_signups: "The count of sign_up events that the user generated during the evaluated time period.",
    total_tutorials_started: "The count of tutorial_begin events that the user generated during the evaluated time period.",
    total_tutorials_completed: "The count of tutorial_complete events that the user generated during the evaluated time period.",
    total_virtual_currency_spends: "The count of spend_virtual_currency events that the user generated during the evaluated time period.",
    total_virtual_currency_earns: "The count of earn_virtual_currency events that the user generated during the evaluated time period.",
    total_example_pages_viewed: "This is an example attribute to demonstrate how to sum page_views on a specific portion of a website."
  },
  assertions: {
    uniqueKey: ["event_id"]
  }
}

pre_operations {

  DECLARE event_date_checkpoint DEFAULT (
    SELECT date("2020-01-01")
  );

  SET event_date_checkpoint = (
    ${when(incremental(),
      `SELECT
        LEAST(
          (SELECT date_sub(current_date(), INTERVAL ${dataform.projectConfig.vars.ga4_days_back} DAY)),
          (SELECT max(event_date) from ${self()})
        )`,
      `SELECT date("${dataform.projectConfig.vars.ga4_start_date}")`
    )}
  );

  ${when(incremental(),
    `DELETE ${self()} WHERE event_date > event_date_checkpoint`
  )}  
}

WITH events AS (
  SELECT * EXCEPT(
      --Remove duplicates
      full_session_id,
      primary_user_id
    ),
    full_session_id AS events_full_session_id,
    primary_user_id AS events_primary_user_id
  FROM ${ref("output_ga4_events")}
  WHERE event_date > event_date_checkpoint
),

sessions AS (
  SELECT * EXCEPT(
    --Remove duplicates
    primary_user_id,
    user_id
    )
  FROM ${ref("output_ga4_sessions")}
  WHERE session_date > event_date_checkpoint
),

users AS (
  SELECT *
  FROM ${ref("output_ga4_users")}
),

joined_table AS (
  SELECT * EXCEPT(events_full_session_id,events_primary_user_id)
  FROM events t1
  JOIN sessions t2 ON t1.events_full_session_id = t2.full_session_id
  JOIN users t4 ON t1.events_primary_user_id = t4.primary_user_id
)

SELECT * 
FROM joined_table
